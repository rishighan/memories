pkgdatadir = get_option('prefix') / get_option('datadir') / meson.project_name()
moduledir = pkgdatadir / 'memories'
gnome = import('gnome')

gnome.compile_resources('memories',
  'memories.gresource.xml',
  gresource_bundle: true,
  install: true,
  install_dir: pkgdatadir,
)

python = import('python')

conf = configuration_data()
conf.set('PYTHON', python.find_installation('python3').full_path())
conf.set('VERSION', meson.project_version())
conf.set('localedir', get_option('prefix') / get_option('localedir'))
conf.set('pkgdatadir', pkgdatadir)

configure_file(
  input: 'memories.in',
  output: 'memories',
  configuration: conf,
  install: true,
  install_dir: get_option('bindir'),
  install_mode: 'rwxr-xr-x'
)

memories_sources = [
  'main.py',
  'window.py',
  'memos_api.py',
  'memo_row.py',
  'memo_loader.py',
  'settings.py',
  'utils/connection_handler.py',
  'ui/connection_view.py',
  'ui/memos_view.py',
]

install_data(memories_sources, install_dir: moduledir)
